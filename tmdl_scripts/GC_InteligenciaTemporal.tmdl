createOrReplace

	table GC_InteligenciaTemporal

		calculationGroup

			calculationItem Atual = SELECTEDMEASURE()

			calculationItem 'Ano Anterior' = ```
										
					CALCULATE(
                        SELECTEDMEASURE(),
                        SAMEPERIODLASTYEAR(DimCalendario[Data])
                    )
					
					```

			calculationItem 'Ano Atual vs Anterior' = ```
					
					
					VAR __Anterior = 
					    CALCULATE( 
					        SELECTEDMEASURE(),
					        GC_InteligenciaTemporal[Item] = "Ano Anterior"
					    )
					
					VAR __Resultado = 
					    DIVIDE(
					        SELECTEDMEASURE() - __Anterior,
					        ABS(__Anterior)
					    )
					
					RETURN 
                        __Resultado 
					        
					```

				formatStringDefinition = ```
						"0.0%;-0.0%" 
						```

			calculationItem 'Mês Anterior' = ```
					
					CALCULATE(
                        SELECTEDMEASURE(),
                        PREVIOUSMONTH(DimCalendario[Data])
                    ) 
						
					
					```

			calculationItem 'Mês Atual vs Anterior' = ```
					
					
					VAR __Anterior = 
					    CALCULATE( 
					        SELECTEDMEASURE(),
					        GC_InteligenciaTemporal[Item] = "Mês Anterior"
					    )
					
					VAR __Resultado = 
					    DIVIDE(
					        SELECTEDMEASURE() - __Anterior,
					        ABS(__Anterior)
					    )
					
					RETURN 
                        __Resultado 

					```

				formatStringDefinition = ```
						"0.0%;-0.0%" 
						```

			calculationItem 'Semana Anterior' = ```
					
					CALCULATE(
                        SELECTEDMEASURE(),
                        OFFSET(
                            -1,
                            ALLSELECTED(
                                DimCalendario[SemanaAnoNomeISO], 
                                DimCalendario[SemanaAnoNumeroISO] 
                            ),
                            ORDERBY(DimCalendario[SemanaAnoNumeroISO], ASC)
                        )
                    ) 
					
					
					```

			calculationItem 'Semana Atual vs Anterior' = ```
					
					
					VAR __Atual = SELECTEDMEASURE()
					VAR __Anterior = 
					    CALCULATE(
					        SELECTEDMEASURE(),
					        GC_InteligenciaTemporal[Item] = "Semana Anterior"
					    ) 
					
					VAR __Resultado =
					    DIVIDE(
					        __Atual - __Anterior,
					        ABS( __Anterior)
					    ) 
					
					RETURN
                        __Resultado

					```

				formatStringDefinition = "0.0%;-0.0%"

			calculationItem 'Dia Anterior' = ```
					
					    CALCULATE(
					        SELECTEDMEASURE(),
					        DATEADD( 
					            DimCalendario[Data],
					            -1,
					            DAY
					        )
					    ) 

					```

			calculationItem 'Dia Atual vs Anterior' = ```
					
					
					VAR __Atual = SELECTEDMEASURE()
					VAR __Anterior = 
					    CALCULATE(
					        SELECTEDMEASURE(),
					        GC_InteligenciaTemporal[Item] = "Dia Anterior"
					    ) 
					
					VAR __Resultado =
					    DIVIDE(
					        __Atual - __Anterior,
					        ABS(__Anterior)
					    ) 
					
					RETURN 
                        __Resultado 
					```

				formatStringDefinition = ```
						"0.0%;-0.0%" 
						```

			calculationItem 'Trimestre Anterior' = ```			

                    CALCULATE(
                        SELECTEDMEASURE(),
                        DATEADD( 
                            DimCalendario[Data],
                            -1,
                            QUARTER
                        )
                    ) 
										
					```

			calculationItem 'Trimestre Atual vs Anterior' = ```
					
					
					VAR __Atual = SELECTEDMEASURE()
					VAR __Anterior = 
					    CALCULATE(
					        SELECTEDMEASURE(),
					        GC_InteligenciaTemporal[Item] = "Trimestre Anterior"
					    ) 
					
					VAR __Resultado =
					    DIVIDE(
					        __Atual - __Anterior,
					        ABS( __Anterior)
					    ) 
					
					RETURN 
                        __Resultado 

					```

			calculationItem 'Média Móvel em Dias' = ```
										
					VAR __Qtd = 3 
					
					VAR __InicioRel = -1 * (__Qtd - 1)
					
					VAR __relacao = 
					    ALLSELECTED(DimCalendario[Data])
					
					VAR __tabela =
					    WINDOW(
					        __InicioRel, REL,
					        0, REL,
					        __relacao,
					        ORDERBY(DimCalendario[Data])
					    )
					
					VAR __Resultado = 
					    AVERAGEX( 
					        __tabela,
					        SELECTEDMEASURE()  
					    )
					
					RETURN 
                        __Resultado 

					```

			calculationItem 'Média Móvel em Semanas' = ```
										
					VAR __Qtd = 3
					
					VAR __InicioRel = -1 * (__Qtd - 1)
					
					VAR __relacao = 
					    ALLSELECTED(DimCalendario[SemanaAnoNomeISO], DimCalendario[SemanaAnoNumeroISO])
					
					VAR __tabela =
					    WINDOW(
					        __InicioRel, REL,
					        0, REL,
					        __relacao,
					        ORDERBY( DimCalendario[SemanaAnoNumeroISO])
					    )
					
					VAR __Resultado = 
					    AVERAGEX( 
					        __tabela,
					        SELECTEDMEASURE()
					    )
					
					RETURN 
                        __Resultado 

					```

			calculationItem 'Média Móvel em Meses' = ```
					
					VAR __Qtd = 3
					
					VAR __InicioRel = -1 * (__Qtd - 1)
					
					VAR __relacao = 
					    ALLSELECTED(DimCalendario[MesAnoNome], DimCalendario[MesAnoNumero])
					
					VAR __tabela =
					    WINDOW(
					        __InicioRel, REL,
					        0, REL,
					        __relacao,
					        ORDERBY(DimCalendario[MesAnoNumero])
					    )
					
					VAR __Resultado = 
					    AVERAGEX( 
					        __tabela,
					        SELECTEDMEASURE() 
					    )
					
					RETURN 
                        __Resultado 
					
					```

			calculationItem 'Média Móvel em Trimestres' = ```
										
					VAR __Qtd = 3
					
					VAR __InicioRel = -1 * (__Qtd - 1)
					
					VAR __relacao = 
					    ALLSELECTED(DimCalendario[TrimestreAnoNome], DimCalendario[TrimestreAnoNumero])
					
					VAR __tabela =
					    WINDOW(
					        __InicioRel, REL,
					        0, REL,
					        __relacao,
					        ORDERBY(DimCalendario[TrimestreAnoNumero])
					    )
					
					VAR __Resultado = 
					    AVERAGEX( 
					        __tabela,
					        SELECTEDMEASURE() 
					    )
					
					RETURN 
                        __Resultado 

					```

            calculationItem 'Acumulado' = ```
										
					VAR __DataContexto = 
                        MAX(DimCalendario[Data])
					
					VAR __Resultado = 
					    CALCULATE(
					        SELECTEDMEASURE(),
					        DimCalendario[Data] <= __DataContexto
					    )
					
					RETURN 
                        __Resultado 

					```

			calculationItem 'Acumulado Anual' = ```
										
					CALCULATE( 
                        SELECTEDMEASURE(),
                        DATESYTD(DimCalendario[Data])
                    )
					
					```
		
			calculationItem 'Acumulado Mensal' = ```
										
                    CALCULATE(
                        SELECTEDMEASURE(),
                        DATESMTD(DimCalendario[Data])
                    )
					
					```

			calculationItem 'Acumulado Trimestral' = ```
					
                    CALCULATE(
                        SELECTEDMEASURE(),
                        DATESQTD(DimCalendario[Data])
                    )		
					    
					```

		column Item
			dataType: string
			summarizeBy: none
			sourceColumn: Name
			sortByColumn: Ordinal

			annotation SummarizationSetBy = Automatic

		column Ordinal
			dataType: int64
			formatString: 0
			summarizeBy: none
			sourceColumn: Ordinal

			annotation SummarizationSetBy = Automatic